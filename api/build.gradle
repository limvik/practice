plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.limvik'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

swaggerSources {
	def typeMappings = 'URI=URI'
	def importMappings = 'URI=java.net.URI'
	eStore {
		def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
		def configJson = "${rootDir}/src/main/resources/api/config.json"
		inputFile = file(apiYaml)
		def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
		code {
			language = 'spring'
			configFile = file(configJson)
			rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings', typeMappings,
						  '--import-mappings', importMappings] as List<String>
			components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
		}
	}
}

compileJava.dependsOn swaggerSources.eStore.code
sourceSets.main.java.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/resources"

processResources {
    dependsOn 'generateSwaggerCodeEStore'
}

repositories {
	mavenCentral()
}

dependencies {
	// https://mvnrepository.com/artifact/org.openapitools/openapi-generator-cli
	swaggerCodegen 'org.openapitools:openapi-generator-cli:6.6.0'
	// https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-annotations
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.12'
	// https://mvnrepository.com/artifact/org.openapitools/jackson-databind-nullable
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	// https://mvnrepository.com/artifact/javax.validation/validation-api
	implementation 'javax.validation:validation-api:2.0.1.Final'
	// https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	// https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
	implementation 'javax.xml.bind:jaxb-api:2.3.1'
	// https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}